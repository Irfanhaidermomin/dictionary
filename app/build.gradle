apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    def DEBUG = readProps('app/config/debug.properties')
    def RELEASE = readProps('app/config/release.properties')

    defaultConfig {
        applicationId 'com.italankin.dictionary'
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 16
        versionName '1.7.1'
        archivesBaseName = "dictionary-$versionName"
    }

    signingConfigs {
        debug {
            storeFile file(DEBUG['keystore'])
            storePassword DEBUG['keystorePasswd']
            keyAlias DEBUG['alias']
            keyPassword DEBUG['aliasPasswd']
        }
        release {
            if (RELEASE != null) {
                storeFile file(RELEASE['keystore'])
                storePassword RELEASE['keystorePasswd']
                keyAlias RELEASE['alias']
                keyPassword RELEASE['aliasPasswd']
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug

            buildConfigField('String', 'API_KEY', DEBUG['apiKey'])
            buildConfigField('String', 'BASE_URL', DEBUG['baseUrl'])
        }
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release

            if (RELEASE != null) {
                buildConfigField('String', 'API_KEY', RELEASE['apiKey'])
                buildConfigField('String', 'BASE_URL', RELEASE['baseUrl'])
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
}

dependencies {
    compile 'io.reactivex:rxjava:1.2.0'
    compile 'io.reactivex:rxandroid:1.2.1'

    compile 'com.google.dagger:dagger:2.6'
    apt 'com.google.dagger:dagger-compiler:2.6'
    provided 'javax.annotation:jsr250-api:1.0'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:support-v4:25.0.1'
    compile 'com.android.support:cardview-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'

    compile 'de.psdev.licensesdialog:licensesdialog:1.8.1'
}

/**
 * Construct new properties object and read it from file.
 *
 * @param fileName name of the file
 * @return properties
 */
Properties readProps(String fileName) {
    File file = new File(fileName)
    if (file.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file))
        return props
    }
    return null
}
