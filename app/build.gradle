apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    def DEBUG = readProps('app/config/debug.properties')
    def RELEASE = readProps('app/config/release.properties')

    defaultConfig {
        applicationId 'com.italankin.dictionary'
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 2
        versionName '1.1'
    }

    signingConfigs {
        debug {
            storeFile file(DEBUG['keystore'])
            storePassword DEBUG['keystorePasswd']
            keyAlias DEBUG['alias']
            keyPassword DEBUG['aliasPasswd']
        }
        release {
            storeFile file(RELEASE['keystore'])
            storePassword RELEASE['keystorePasswd']
            keyAlias RELEASE['alias']
            keyPassword RELEASE['aliasPasswd']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug

            buildConfigField('String', 'API_KEY', DEBUG['apiKey'])
            buildConfigField('String', 'BASE_URL', DEBUG['baseUrl'])
        }
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.release

            buildConfigField('String', 'API_KEY', RELEASE['apiKey'])
            buildConfigField('String', 'BASE_URL', RELEASE['baseUrl'])
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'

    compile 'com.squareup.retrofit2:retrofit:2.0.0'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.0'

    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.android.support:cardview-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
}

Properties readProps(String file) {
    Properties props = new Properties()
    props.load(new FileInputStream(file))
    return props;
}
